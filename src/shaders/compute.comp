#version 450
#extension GL_ARB_separate_shader_objects : enable

// Configuration options for culling
#define ENABLE_ORIENTATION_CULLING true
#define ORIENTATION_CULLING_THRESHOLD 0.9 // This is the threshold for culling blades based on their orientation 

#define ENABLE_FRUSTUM_CULLING true
#define FRUSTUM_EPS 0.1

#define ENABLE_DISTANCE_CULLING true
#define DISTANCE_MAX 50.0
#define NUM_BUCKETS 15 

#define WORKGROUP_SIZE 32
layout(local_size_x = WORKGROUP_SIZE, local_size_y = 1, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform CameraBufferObject {
    mat4 view;
    mat4 proj;
    vec4 pos; 
} camera;

layout(set = 1, binding = 0) uniform Time {
    float deltaTime;
    float totalTime;
};

struct Blade {
    vec4 v0;
    vec4 v1;
    vec4 v2;
    vec4 up;
};

// TODO: Add bindings to:
// 1. Store the input blades
// 2. Write out the culled blades
// 3. Write the total number of blades remaining

// The project is using vkCmdDrawIndirect to use a buffer as the arguments for a draw call
// This is sort of an advanced feature so we've showed you what this buffer should look like
//
// layout(set = ???, binding = ???) buffer NumBlades {
// 	  uint vertexCount;   // Write the number of blades remaining here
// 	  uint instanceCount; // = 1
// 	  uint firstVertex;   // = 0
// 	  uint firstInstance; // = 0
// } numBlades;

layout(set = 2, binding = 0) buffer InputBladesBuffer {
    Blade inputBlades[];
};

layout(set = 2, binding = 1) buffer OutputBladesBuffer {
    Blade outputBlades[];
};

layout(set = 2, binding = 2) buffer NumBlades {
 	uint vertexCount;   // Write the number of blades remaining here
 	uint instanceCount; // = 1
 	uint firstVertex;   // = 0
 	uint firstInstance; // = 0
} numBlades;

bool inBounds(float value, float bounds) {
    return (value >= -bounds) && (value <= bounds);
}

vec3 GetCameraFrontWorld(mat4 viewMat) {
    return normalize(-viewMat[2].xyz);
}

bool inFrustumClip(vec4 clip, float eps) {
    float w = clip.w;

    if (clip.x < -w - eps) return false;
    if (clip.x >  w + eps) return false;
    if (clip.y < -w - eps) return false;
    if (clip.y >  w + eps) return false;

    if (clip.z < 0.0 - eps) return false;
    if (clip.z >  w + eps) return false;

    return true;
}

void main() {
	// Reset the number of blades to 0
	if (gl_GlobalInvocationID.x == 0) {
		 numBlades.vertexCount = 0;
	}
	barrier(); // Wait till all threads reach this point

    // TODO: Apply forces on every blade and update the vertices in the buffer

	// DONE: Cull blades that are too far away or not in the camera frustum and write them
	// to the culled blades buffer
    
    Blade blade = inputBlades[gl_GlobalInvocationID.x];
    vec3 v0 = blade.v0.xyz; 
    vec3 v1 = blade.v1.xyz;
    vec3 v2 = blade.v2.xyz;
    vec3 up = normalize(blade.up.xyz);
    
    // Cull the blade if it is not facing the camera
    if(ENABLE_ORIENTATION_CULLING) {
        float theta = blade.v0.w;  
        vec3 grassLeft = vec3(cos(theta), 0, sin(theta)); // Assume up vector is alwyas (0, 1, 0)
        vec3 camFront = GetCameraFrontWorld(camera.view);
        float dotProduct = dot(grassLeft, camFront);
        if (abs(dotProduct)> ORIENTATION_CULLING_THRESHOLD) {
            return; // Skip this blade
        }
    }
    
    mat4 viewProj = camera.proj * camera.view; 
    // Cull the blade if it is outside of the frustum
    if(ENABLE_FRUSTUM_CULLING) {
        vec3 midPos = (0.25 * v0 +
                       0.50 * v1 +
                       0.25 * v2);

        vec4 p0Clip  = viewProj * vec4(v0, 1.0);
        vec4 p2Clip  = viewProj * vec4(v2, 1.0);
        vec4 midClip = viewProj * vec4(midPos,       1.0);

        bool visible = false;
        if (inFrustumClip(p0Clip,  FRUSTUM_EPS)) visible = true;
        if (inFrustumClip(p2Clip,  FRUSTUM_EPS)) visible = true;
        if (inFrustumClip(midClip, FRUSTUM_EPS)) visible = true;

        if (!visible) {
            return;
        }
    }

    if(ENABLE_DISTANCE_CULLING) {
        vec3 cameraPos = camera.pos.xyz; 
        float dProj = length(v0 - cameraPos - up * dot((v0 - cameraPos), up));
        bool culled = mod(gl_GlobalInvocationID.x, NUM_BUCKETS) > floor(NUM_BUCKETS * (1 - dProj / DISTANCE_MAX)); 

        if(culled) {
            return; 
        }
    }

	// Note: to do this, you will need to use an atomic operation to read and update numBlades.vertexCount
	// You want to write the visible blades to the buffer without write conflicts between threads
    const uint bladeIdx = atomicAdd(numBlades.vertexCount, 1);
    outputBlades[bladeIdx] = inputBlades[gl_GlobalInvocationID.x];
}
